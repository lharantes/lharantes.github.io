---
title: "Azure Landing Zone: criar grupos no Entra ID e adicionar permissÃµes com Terraform"
date: 2025-06-20 01:00:00
categories: [Devops, Terraform]
tags: [devops, azure, terraform]
slug: 'azure-landing-zone-groups-and-role-assignments'
image:
  path: assets/img/39/39-header.webp
---

OlÃ¡ pessoal! Blz?

Eu quero trazer uma sÃ©rie de artigos em que tenho trabalhado que Ã© a criaÃ§Ã£o de cÃ³digo Terraform para setup de uma **Azure Landing Zone**, pouco tempo atrÃ¡s a Microsoft disponibilizou o <a href="https://learn.microsoft.com/en-us/azure/cloud-adoption-framework/ready/landing-zone/deploy-landing-zones-with-terraform" target="_blank">Aceleradores de Landing Zone do Azure para Bicep e Terraform</a>, mas eu preferi criar meus prÃ³prios mÃ³dulos conforme vou precisando e moldando eles da forma que preciso, com isso me forÃ§o a estudar e praticar o uso de Terraform.

Teremos um conjunto de artigos onde no final teremos a possibilidade de automatizar uma Azure Landing Zone e moldar isso como precisamos. Talvez esses meus artigos sejam para ambientes pequenos e mÃ©dios, onde nÃ£o temos uma Azure Landing Zone tÃ£o segregada com vÃ¡rias Assinaturas e Grupos de Gerenciamentos.

O primeiro artigo gostaria de demonstrar como ***criar um grupo no Microsoft Entra ID, adicionar membros a esse grupo e depois adicionar permissÃµes Azure RBAC a esse grupo*** tudo isso atravÃ©s de Terraform.

> No Ãºltimo artigo vou "juntar" os mÃ³dulos que criamos no decorrer dessa sÃ©rie e montar um projeto Terraform para automatizar a criaÃ§Ã£o da Azure Landing Zone.
{: .prompt-tip }

VocÃª pode ir conferindo o cÃ³digo no <a href="https://github.com/lharantes/azure-landing-zone-terraform" target="_blank">repositÃ³rio no GitHub</a>.


## O que Ã© uma Azure Landing Zone

Uma Azure Landing Zone Ã© um conjunto de prÃ¡ticas, configuraÃ§Ãµes e recursos prÃ©-projetados no Microsoft Azure que serve como ponto de partida para implantar cargas de trabalho na nuvem de forma segura, escalÃ¡vel e alinhada a boas prÃ¡ticas de governanÃ§a.

VocÃª pode pensar nela como o terreno preparado antes de construir uma cidade: jÃ¡ vem com ruas, luz, Ã¡gua e regras de zoneamento. Na nuvem, isso significa que jÃ¡ existem configuraÃ§Ãµes prontas para:

- **Estrutura de rede (VNets, sub-redes, conectividade hÃ­brida etc.)**

- **SeguranÃ§a (polÃ­ticas, RBAC, controle de acesso e criptografia)**

- **GovernanÃ§a (Azure Policy, tagging e gerenciamento de assinaturas)**

- **Monitoramento (Log Analytics, alertas e diagnÃ³sticos)**

- **Identidade (integraÃ§Ã£o com Azure AD, roles e MFA)**

O principal objetivo Ã© garantir que todas as futuras cargas de trabalho implantadas no Azure sigam um padrÃ£o seguro, bem organizado e em conformidade com os requisitos da empresa ou regulatÃ³rios.

Abaixo uma imagem da Microsoft onde demonstra o uso de uma Azure Landing Zone:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/01.svg){: .shadow .rounded-10}

## Role-based access control (Azure RBAC) 

O gerenciamento de acesso para recursos de nuvem Ã© uma funÃ§Ã£o crÃ­tica para qualquer organizaÃ§Ã£o que esteja usando a nuvem. O RBAC do Azure (controle de acesso baseado em funÃ§Ãµes do Azure) ajuda a gerenciar quem tem acesso aos recursos do Azure, o que pode fazer com esses recursos e a quais Ã¡reas tem acesso.

Nesse artigo como mencionei acima vamos criar as permissÃµes da forma que esperamos que ela seja gerenciada, ou seja, atravÃ©s de grupos e adicionando os usuÃ¡rios a esse grupo. Usar grupos para conceder permissÃµes no Azure Ã© considerado uma boa prÃ¡tica porque traz organizaÃ§Ã£o, seguranÃ§a e eficiÃªncia, pois seria somente adicionar e/ou remover usuÃ¡rios dos grupos quando ele nÃ£o mais precisa daquela permissÃ£o.

No Azure, vocÃª atribui permissÃµes em um escopo e um escopo em quatro nÃ­veis: **grupo de gerenciamento**, **assinatura**, **grupo de recursos** ou **recurso**. Os escopos sÃ£o estruturados em uma relaÃ§Ã£o pai-filho. VocÃª pode atribuir funÃ§Ãµes em qualquer um desses nÃ­veis de escopo que o nÃ­vel abaixo irÃ¡ herdar essas permissÃµes.

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/02.png){: .shadow .rounded-10}

> Aqui estou dando as dicas de como fazer, procure estruturar isso da melhor forma para o seu ambiente, **SE** no seu ambiente a melhor opÃ§Ã£o Ã© atribuir a permissÃ£o no **Grupo de Gerenciamento** entÃ£o faÃ§a, tem ambientes que atribuir a permissÃ£o na Assinatura Ã© o melhor cenÃ¡rio. Lembre-se antes de criticar: **"Cada ambiente tem a sua particularidade"**.
{: .prompt-info }

Abaixo algumas sugestÃµes de como eu organizo os grupos e permissÃµes iniciais, depois cada caso Ã© um caso e vai adaptando conforme a necessidade:

| Nome do Grupo | PermissÃ£o | DescriÃ§Ã£o |
| ------------- | ------------ | --------- |
| GRP-Owner     | Owner     | Acesso Irrestrito no escopo definido |
| GRP-Infra     | Contributor     | Criar recursos no escopo definido |
| GRP-Sustentacao    | Reader     | Acesso de leitura no escopo definido |
| GRP-Finops    | Cost Management Contributor     | Administrar o custo no escopo definido |
| GRP-Terceiros    | Contributor     | Criar recursos no escopo definido |

## OrganizaÃ§Ã£o do cÃ³digo Terraform

Nesses artigos vamos organizar nossos mÃ³dulos Terraform da forma abaixo, onde temos uma pasta mÃ³dulos e depois uma pasta para cada item que formos criar, onde teremos distribuidos da seguinte forma:

- **main.tf**: onde serÃ¡ criado o/os recursos
- **variables.tf**: onde ficarÃ¡ as variÃ¡veis necessÃ¡rias
- **output.tf**: as saÃ­das dos recursos para serem reutilizados por outros recursos

```shell
ðŸ“¦modules
 â”— ðŸ“‚entra_id_groups
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ ðŸ“‚az_role_assignment
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
```

Os cÃ³digos abaixo estÃ£o separados para usarmos em mÃ³dulos, e o principal objetivo do uso de mÃ³dulos em Terraform Ã© podermos reaproveitÃ¡-los.

## Automatizar a criaÃ§Ã£o de Grupos no Entra Id com Terraform

> Em ambientes hÃ­bridos onde os grupos e usuÃ¡rios sÃ£o geralmente sincronizados entre um **Active Directory local** e o **Azure** vocÃª nÃ£o precisa recriar os grupos e sim **usar ele mais adiante** quando chegar na parte de dar permissÃµes.
{: .prompt-info }

No Microsoft Azure vocÃª pode dar permissÃµes usando usuÃ¡rio, grupos de usuÃ¡rios e service principal, nesse exemplo e como uma boa prÃ¡tica iremos dar permissÃµes a grupos, para isso vamos criar um grupo de usuÃ¡rio e inserir usuÃ¡rios nesse grupo, e faremos isso usando Terraform.

**main.tf**

```hcl
# Recuperar do Entra ID os usuÃ¡rios que serÃ£o proprietÃ¡rios do grupo a ser criado
data "azuread_users" "owners" {
  user_principal_names = var.owners
}

# Recuperar do Entra ID os usuÃ¡rios que serÃ£op membros do grupo que serÃ¡ criado
data "azuread_user" "user_id" {
  for_each            = toset(var.member_id)
  user_principal_name = each.key
}

resource "azuread_group" "this" {
  display_name            = var.group_name
  owners                  = data.azuread_users.owners.object_ids
  security_enabled        = var.security_enabled
  assignable_to_role      = var.assignable_to_role
  prevent_duplicate_names = true
  types                   = var.group_type

  # Example = "user.department -eq \"IT\""
  dynamic "dynamic_membership" {
    for_each = var.group_type != [] ? var.group_type : null
    content {
      enabled = true
      rule    = var.dynamic_membership_rule
    }
  }
}

# Adicionando os usuÃ¡rios ao grupo criado anteriormente
resource "azuread_group_member" "this" {
  for_each         = var.member_id == [] ? [] : toset(var.member_id)
  group_object_id  = azuread_group.this.object_id
  member_object_id = data.azuread_user.user_id[each.value].object_id
}
```

**variables.tf**

```hcl
variable "security_enabled" {
  type        = bool
  default     = true
  description = "Whether the group is a security group for controlling access to in-app resources"
}

variable "group_name" {
  type        = string
  description = "The display name for the group."
}

variable "assignable_to_role" {
  type        = bool
  default     = false
  description = "Indicates whether this group can be assigned to an Azure Active Directory role. Can only be set to true for security-enabled groups."
}

variable "owners" {
  type        = list(string)
  default     = []
  description = "A set of object IDs of principals that will be granted ownership of the group. Supported object types are users or service principals."
}

variable "group_type" {
  type    = list(string)
  default = []
  description = "A set of group types to configure for the group. Set it for DynamicMembership when want the group to be dynamic in Entra ID"
}

variable "dynamic_membership_rule" {
  type        = string
  default     = ""
  description = "Used when you want the group to be dynamic in Entra IDâ€”that is, for membership to be automatically determined based on conditions and attributes of users or devices."
}

variable "member_id" {
  type    = list(string)
  default = []
  description = "A set of members who should be present in this group. Supported object types are Users, Groups or Service Principals. Cannot be used with the dynamic_membership block."
}
```

**output.tf**

```hcl
output "group_objecti_id" {
  value       = azuread_group.this.object_id
  description = "Group object ID"
}

output "group_name" {
  value       = azuread_group.this.display_name
  description = "Group name"
}

```

## Dar permissÃ£o RBAC no Azure usando Terraform

No cÃ³digo acima criamos o grupo no Entra ID e inserimos os membros dentro do grupo, no cÃ³digo Terraform abaixo iremos dar permissÃ£o a esse grupo no Microsoft Azure. 

JÃ¡ explicamos que a permissÃ£o Ã© concedida ao nÃ­vel de escopo, para isso devemos ter um cuidado a usar a formataÃ§Ã£o necessÃ¡rias para especificar os escopos, pois Ã© necessÃ¡rio que estejam da maneira abaixo:

- **Assinaturas:** /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333
- **Grupo de Recurso:** /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333/resourceGroups/**myGroup**
- **Recurso:** /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333/resourceGroups/myGroup/providers/Microsoft.Compute/virtualMachines/**myVM**
- **Grupo de Gerenciuamento:** /providers/Microsoft.Management/managementGroups/**myMG**

> O negrito no final do exemplo de escopo Ã© o nome do recurso.
{: .prompt-tip }


Abaixo o cÃ³digo Terraform para criarmos o mÃ³dulo de **role assignment:**

**locals.tf**
```hcl
locals {
  role_definition_resource_substring = "/providers/Microsoft.Authorization/roleDefinitions"
}
```

**main.tf**
```hcl
resource "azurerm_role_assignment" "this" {
  for_each = var.role_assignments

  principal_id                     = each.value.principal_id
  scope                            = var.scope
  principal_type                   = each.value.principal_type
  role_definition_id               = strcontains(lower(each.value.role_definition_id_or_name), lower(local.role_definition_resource_substring)) ? each.value.role_definition_id_or_name : null
  role_definition_name             = strcontains(lower(each.value.role_definition_id_or_name), lower(local.role_definition_resource_substring)) ? null : each.value.role_definition_id_or_name
  skip_service_principal_aad_check = each.value.skip_service_principal_aad_check
}
```

**variables.tf**
```hcl
variable "scope" {
  type        = string
  description = <<DESCRIPTION
  The scope at which the Role Assignment applies to, such as /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333, /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333/resourceGroups/myGroup, or /subscriptions/0b1f6471-1bf0-4dda-aec3-111122223333/resourceGroups/myGroup/providers/Microsoft.Compute/virtualMachines/myVM, or /providers/Microsoft.Management/managementGroups/myMG.
  DESCRIPTION
}

variable "role_assignments" {
  type = map(object({
    role_definition_id_or_name       = string
    principal_id                     = string
    description                      = optional(string, null)
    skip_service_principal_aad_check = optional(bool, false)
    principal_type                   = optional(string, null)
  }))
  default     = {}
  description = <<DESCRIPTION
A map of role assignments to create on the Key Vault. The map key is deliberately arbitrary to avoid issues where map keys maybe unknown at plan time.

- `role_definition_id_or_name` - The ID or name of the role definition to assign to the principal.
- `principal_id` - The ID of the principal to assign the role to.
- `description` - The description of the role assignment.
- `skip_service_principal_aad_check` - If set to true, skips the Azure Active Directory check for the service principal in the tenant. Defaults to false.

> Note: only set `skip_service_principal_aad_check` to true if you are assigning a role to a service principal.
DESCRIPTION
  nullable    = false
}
```

## Como usar os mÃ³dulos Terraform

Nesse primeiro artigo jÃ¡ temos dois mÃ³dulos construÃ­dos, uma para criar grupos no Entra Id e adicionar usuÃ¡rios ao grupo e outro para adicionar a permissÃ£o ao Microsoft Azure, para usarmos esses mÃ³dulos tem um arquivo ***main.tf*** fora da pasta mÃ³dulos (root folder) onde iremos fazer a chamada aos mÃ³dulos criados:

```shell
ðŸ“¦Azure_Landing_Zone
 â”£ ðŸ“‚modules
 â”ƒ â”£ ðŸ“‚az_role_assignment
 â”ƒ â”ƒ â”£ ðŸ“œlocals.tf
 â”ƒ â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ â”— ðŸ“‚entra_id_groups
 â”ƒ â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”ƒ â”— ðŸ“œvariables.tf
 â”£ ðŸ“œmain.tf <----------
```

No exemplo abaixo estamos dando permissÃ£o de **Reader** ao grupo **GRP-TI** a uma Assinatura do Azure:

```hcl
variable "subscription_id" {
  type    = string
  default = "12345678-0000-1111-2222-123456789098"
}

module "group_ti" {
  source = "./entra_id_groups"

  group_name = "GRP-TI"
  owners     = ["luiz@arantes.net.br"]
  member_id  = ["luiz@arantes.net.br", "julia@arantes.net.br"]
}

module "subscription_assignment" {
  source = "./az_role_assignment"

  scope = "/subscriptions/${var.subscription_id}"
  role_assignments = {
    subscription = {
      principal_id               = module.group_ti.group_objecti_id
      role_definition_id_or_name = "Reader"
    }
  }
}
```

> Lembrando que para dar permissÃµes no Microsoft Azure precisamos ter a permissÃ£o de **Owner** ou **User Access Administrator** para quem estiver executando o cÃ³digo.
{: .prompt-warning }

Verificando no Microsoft Entra ID se o grupo foi criado e os membros associados:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/03.png){: .shadow .rounded-10}

E verificando tambÃ©m se a permissÃ£o foi dada na Assinatura no Microsoft Azure:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/04.png){: .shadow .rounded-10}

## Atribuindo permissÃµes a grupos em um ambiente hÃ­brido

Se vocÃª tem um ambiente hÃ­brido onde hÃ¡ a sincronizaÃ§Ã£o do Active directory local com o Azure vocÃª nÃ£o irÃ¡ recriar os grupos existentes, vocÃª precisa apenas do **object ID** desse grupo e referenciar esse ID no terraform ao dar permissÃ£o no Microsoft Azure.

Para pegar esse ID vocÃª deve ir no ***Microsoft Entra ID -> Grupos -> Todos os grupos*** e selecionar o grupo que deseja visualizar o ID:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/05.png){: .shadow .rounded-10}

Para pegar o Object Id Ã© mais fÃ¡cil abrir o grupo clicando nele e copiar como a imagem abaixo:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/06.png){: .shadow .rounded-10}

Com o ID copiado Ã© sÃ³ inserir em uma variÃ¡vel no Terraform ou diretamente na chamada do mÃ³dulo:

```hcl
module "subscription_assignment" {
  source = "./az_role_assignment"

  scope = "/subscriptions/${var.subscription_id}"
  role_assignments = {
    subscription = {
      principal_id               = "822c0768-938e-4af7-a068-6432f7584cbd"
      role_definition_id_or_name = "Reader"
    }
  }
}
```

ApÃ³s executar o Terraform teremos a permissÃ£o atribuida:

![azure-landing-zone-groups-and-role-assignments](/assets/img/39/07.png){: .shadow .rounded-10}

## Concluindo!

Pode parecer uma coisa simples e sem utilidade, mas quando Ã© necessÃ¡rio automatizar a criaÃ§Ã£o de vÃ¡rios grupos e adicionar usuÃ¡rios a eles, automatizar esse tipo de tarefa nos reduz um grande esforÃ§o. E ter uma Azure Landing Zone com os acessos automatizados nos ajuda a seguir um processo e nÃ£o ter algum erro manual, pense que ao criar uma assinatura nova vocÃª jÃ¡ tem todos os passos para automatizar isso, e nesse primeiro artigo comeÃ§am os demonstrando como criar os grupos no Microsoft Entra ID e dar acesso a eles na assinatura do Microsoft Azure.

Bom pessoal, eu tenho usado isso em alguns ambientes e acredito que possa ser bem Ãºtil a vocÃªs!

## Artigos relacionados

<a href="https://learn.microsoft.com/en-us/azure/cloud-adoption-framework/ready/landing-zone/" target="_blank">What is an Azure landing zone?</a>

<a href="https://learn.microsoft.com/en-us/azure/cloud-adoption-framework/scenarios/app-platform/app-services/landing-zone-accelerator" target="_blank">Deploy Azure App Service at scale with the landing zone accelerator</a>

Compartilhe o artigo com seus amigos clicando nos icones abaixo!!!