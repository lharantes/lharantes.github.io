---
title: "Azure Landing Zone: Azure Backup com Terraform"
date: 2025-08-09 01:00:00
categories: [Devops, Terraform]
tags: [devops, azure, terraform]
slug: 'azure-landing-recovery-service-vault-backup-policy'
image:
  path: /assets/img/42/42-header.webp
---

OlÃ¡ pessoal! Blz?

Dando continuidade a sÃ©rie de artigos para automatizar a configuraÃ§Ã£o de uma **Azure Landing Zone** atravÃ©s de Terraform, neste artigo, vamos ver como criar um **Recovery Services Vault** e configurar uma **polÃ­tica de backup** para mÃ¡quinas virtuais usando Terraform. A ideia Ã© mostrar, de maneira prÃ¡tica, como a infraestrutura como cÃ³digo (IaC) facilita a automaÃ§Ã£o, padronizaÃ§Ã£o e manutenÃ§Ã£o dos seus ambientes, evitando tarefas manuais repetitivas e aumentando a confiabilidade das suas operaÃ§Ãµes na nuvem.

Compartilho aqui a lista de artigos atÃ© aqui para criarmos nossa **Azure Landing Zone com Terraform**: 

- <a href="https://arantes.net.br/posts/azure-landing-zone-groups-and-role-assignments/" target="_blank">Azure Landing Zone: Criar grupos no Entra ID e adicionar permissÃµes com Terraform</a>

- <a href="https://arantes.net.br/posts/azure-landing-zone-naming-tagging/" target="_blank">Azure Landing Zone: Como Nomenclatura e Tags Definem o Sucesso da sua Nuvem</a>

- <a href="https://arantes.net.br/posts/azure-landing-zone-networking/" target="_blank">Azure Landing Zone: Virtual network & Subnet com Terraform</a>

Proteger dados e garantir a disponibilidade de mÃ¡quinas virtuais Ã© essencial em qualquer ambiente na nuvem. No Microsoft Azure, o **Recovery Services Vault** Ã© o serviÃ§o responsÃ¡vel por centralizar e gerenciar backups de forma simples e segura.

A ideia nesse artigo Ã© criar um cofre de backup por regiÃ£o que iremos utilizar, o cofre de backup Ã© um recurso regional e iremos criar uma polÃ­tica de backup,  de acordo com as necessidades de cada ambiente vocÃª pode adaptar a polÃ­tica para ficar de acordo com o compliance do ramo de atividade e/ou empresa.

> Com o serviÃ§o de backup no Azure Ã© possÃ­vel realizar backups tambÃ©m de recursos do ambiente on-premises.
{: .prompt-tip } 

## O que Ã© o Azure Recovery Services Vault

O Azure Recovery Services Vault Ã© um cofre de armazenamento seguro e gerenciado pela Microsoft Azure, usado para armazenar dados de backup e informaÃ§Ãµes de recuperaÃ§Ã£o.
Ele Ã© amplamente utilizado para proteger cargas de trabalho em nuvem e locais (on-premises).

**Principais funÃ§Ãµes:**

- **Backup:** Armazena backups de mÃ¡quinas virtuais (VMs), bancos de dados, arquivos, e outros recursos do Azure.

- **RecuperaÃ§Ã£o (Recovery):** Permite restaurar dados em caso de falha, exclusÃ£o acidental, ataque ransomware ou corrupÃ§Ã£o de dados.

- **CentralizaÃ§Ã£o:** Gerencia mÃºltiplas fontes de backup em um Ãºnico cofre.

- **Criptografia:** Os dados sÃ£o criptografados em trÃ¢nsito e em repouso (com chaves gerenciadas pelo Azure ou pelo cliente).

- **RetenÃ§Ã£o de longo prazo:** MantÃ©m backups por perÃ­odos definidos (dias, semanas, meses ou anos).

**O que pode ser protegido:**

- MÃ¡quinas Virtuais do Azure (Azure VMs)

- Servidores locais (usando o agente MARS ou Azure Backup Server)

- Bancos de dados SQL e SAP HANA

- Compartilhamentos de arquivos (Azure Files)

- Workloads hÃ­bridos (on-premises + nuvem)

## PolÃ­ticas de Backup (Backup Policies)

As **polÃ­ticas de backup** definem **como, quando e por quanto tempo** os backups sÃ£o executados e mantidos no cofre.

**Componentes principais de uma polÃ­tica de backup:**

- **Agendamento de Backup**

  * Define quando o backup Ã© executado (por exemplo, diariamente Ã s 22h).

  * Pode incluir mÃºltiplos pontos de backup diÃ¡rios (atÃ© 3 por dia, dependendo do tipo de recurso).

- **RetenÃ§Ã£o**

  * Determina por quanto tempo cada backup serÃ¡ mantido.

  * Pode incluir polÃ­ticas diferentes:

  * RetenÃ§Ã£o diÃ¡ria (ex: manter por 30 dias)

  * RetenÃ§Ã£o semanal (ex: manter por 12 semanas)

  * RetenÃ§Ã£o mensal (ex: manter por 12 meses)

  * RetenÃ§Ã£o anual (ex: manter por 7 anos)

- **Tipo de backup**

  * Full (completo)

  * Incremental (somente mudanÃ§as desde o Ãºltimo backup, padrÃ£o do Azure)

  * Differential (dependendo do tipo de workload)

- **ConsistÃªncia dos dados**

  * OpÃ§Ãµes para garantir backup consistente com o aplicativo (ex: usar VSS para VMs Windows).

## Vamos imaginar o seguinte cenÃ¡rio

VocÃª tem uma **mÃ¡quina virtual (VM)** no Azure chamada **vm-financeira01** e deseja:

- Fazer **backup diÃ¡rio** Ã s 22h.

- Reter backups por **30 dias**.

- Manter uma cÃ³pia mensal por 12 meses.

- Armazenar tudo no **Recovery Services Vault** chamado **rsv-brs-prd-bck-grs**.

Para implantar esse cenÃ¡rio usaremos o Terraform para criar o cofre de backup (Recovery Services Vault), a polÃ­tica de backup e habilitar o backup em uma mÃ¡quina virtual.

## Script Terraform para criar os recursos

Irei separar os recursos para ir explicando e comentando as mudanÃ§as e como o cÃ³digo estÃ¡ escrito para que cso deseje vocÃª altere para sua realidade.

### Azure Recovery Services Vault

Os cÃ³digos abaixo serÃ£o usados para criar um mÃ³dulo responsÃ¡vem em criar um cofre de backup e a polÃ­tica, depois mostraremos como realizar a chamada ao mÃ³dulo.

Eu gosto de criar um arquivo como ***locals.tf*** para definir alguns parÃ¢metros que usarei no decorrer do cÃ³digo, tenho usado tambÃ©m para ter uma abreviaÃ§Ã£o da regiÃ£o onde serÃ¡ criado o recurso e usar essa abreviaÃ§Ã£o no nome:

***locals.tf***

```hcl
locals {
  regions = {
    "brazilsouth"  = "brs"
    "Brazil South" = "brs"
    "northeurope"  = "ne"
    "North Europe" = "ne"
  }

  replication_type = var.storage_mode_type == "GeoRedundant" ? "grs" : var.storage_mode_type == "LocallyRedundant" ? "lrs : "zrs" 
  short_region     = lookup(local.regions, var.location, false)
  vault_name       = var.environment != null ? lower("rsv-${local.short_region}-${var.environment}-bck-${local.replication_type}") : lower("rsv-${local.short_region}-bck-${local.replication_type}")
}
```

Com a abreviaÃ§Ã£o da regiÃ£o (estamos exemplificando somente a regiÃ£o Brazil South e North Europe) e definindo um padrÃ£o de nomenclatura onde eu nÃ£o pergunto ao usuÃ¡rio o nome do recurso e sim eu faÃ§o uma junÃ§Ã£o de palavras e variÃ¡veis, no nosso exemplo acima e usando como exemplo um ambiente de **prd**, a regiÃ£o **Brazil South** e uma replicaÃ§Ã£o **GeoRedundant**, o nome do cofre de backup ficaria: **rsv-brs-prd-bck-grs**, e se nÃ£o passarmos o ambiente ele nÃ£o colocarÃ¡ o **prd** no nome.

> Eu gosto de usar tambÃ©m o **locals** para a partir de um valor de variÃ¡vel transforma em outro, no exemplo acima estou recebendo o valor **GeoRedundant** na variÃ¡vel **storage_mode_type** e transformando em **grs** para usar na composiÃ§Ã£o do nome do cofre de backup.
{: .prompt-tip } 

***vault.tf***

```hcl
resource "azurerm_recovery_services_vault" "this" {
  name                = local.vault_name
  location            = var.location
  resource_group_name = var.resource_group_name
  sku                 = var.sku

  public_network_access_enabled = var.public_network_access_enabled
  storage_mode_type             = var.storage_mode_type
  cross_region_restore_enabled  = var.cross_region_restore_enabled
  soft_delete_enabled           = var.soft_delete_enabled
  immutability                  = var.immutability

  monitoring {
    alerts_for_all_job_failures_enabled            = true
    alerts_for_critical_operation_failures_enabled = true
  }

  tags = var.tags
}
```

***backup_policy***

```hcl
resource "azurerm_backup_policy_vm" "backup_policy" {
  for_each                       = var.backup_type == "vm" ? var.backup_policies_vm : {}
  name                           = each.key
  resource_group_name            = azurerm_recovery_services_vault.this.resource_group_name
  recovery_vault_name            = azurerm_recovery_services_vault.this.name
  timezone                       = each.value.timezone
  policy_type                    = each.value.policy_type
  instant_restore_retention_days = each.value.instant_restore_retention_days

  backup {
    frequency     = each.value.backup_frequency
    time          = each.value.backup_time
    hour_interval = each.value.backup_frequency == "Daily" ? each.value.backup_hour_interval : null
    hour_duration = each.value.backup_frequency == "Daily" ? each.value.backup_hour_duration : null
    weekdays      = each.value.backup_frequency == "Weekly" ? each.value.retention.weekdays : null
  }

  instant_restore_resource_group {
    prefix = "${azurerm_recovery_services_vault.this.resource_group_name}-"
  }

  dynamic "retention_daily" {
    for_each = each.value.backup_frequency == "Daily" || each.value.retention.daily_backups_retention != null ? [""] : []
    content {
      count = each.value.retention.daily_backups_retention
    }
  }

  dynamic "retention_weekly" {
    for_each = each.value.backup_frequency == "Weekly" || each.value.retention.weekly_backups_retention != null ? [""] : []
    content {
      count    = each.value.retention.weekly_backups_retention
      weekdays = each.value.retention.weekdays
    }
  }

  dynamic "retention_monthly" {
    for_each = each.value.retention.monthly_backups_retention != null ? [""] : []
    content {
      count             = each.value.retention.monthly_backups_retention
      weekdays          = each.value.retention.months_weekdays
      weeks             = each.value.retention.months_weeks
      days              = each.value.retention.months_days
      include_last_days = each.value.retention.include_last_days
    }
  }

  dynamic "retention_yearly" {
    for_each = each.value.retention.yearly_backups_retention != null ? [""] : []
    content {
      count             = each.value.retention.yearly_backups_retention
      months            = each.value.retention.yearly_months
      weekdays          = each.value.retention.yearly_weekdays
      weeks             = each.value.retention.yearly_weeks
      days              = each.value.retention.yearly_days
      include_last_days = each.value.retention.include_last_days
    }
  }
}
```

Abaixo o arquivo de variÃ¡veis usadas no cÃ³digo e cada uma tem a descriÃ§Ã£o para ficar fÃ¡cil a identificaÃ§ao e propÃ³sito:

***variables.tf***

```hcl
variable "environment" {
  type        = string
  description = "Environment project (dev, qua or prd)."
  default     = null
}

variable "backup_type" {
  type        = string
  description = "Type of resources to be backed up. Supported values are 'vm' or 'sql'"
  default     = "vm"

  validation {
    condition     = contains(["vm", "sql"], var.backup_type)
    error_message = "var.backup_type must be one of 'vm' or 'sql'"
  }
}

variable "resource_group_name" {
  description = "The name of the resource group in which to deploy the backup resources."
  type        = string
}

variable "location" {
  description = "Name of location to where backups will be stored"
  type        = string
}

variable "sku" {
  type        = string
  description = "SKU of Recovery Services Vault, either 'Standard' or 'RS0'."
  default     = "Standard"

  validation {
    condition     = contains(["Standard", "RS0"], var.sku)
    error_message = "var.sku must be set to either 'Standard' or 'RS0'"
  }
}

variable "soft_delete_enabled" {
  type        = bool
  description = "Whether to enable soft delete on Recovery Services Vault"
  default     = true
}

variable "storage_mode_type" {
  type        = string
  description = "Storage type of the Recovery Services Vault. Must be one of 'GeoRedundant', 'LocallyRedundant' or 'ZoneRedundant'."
  default     = "LocallyRedundant"

  validation {
    condition     = contains(["GeoRedundant", "LocallyRedundant", "ZoneRedundant"], var.storage_mode_type)
    error_message = "var.storage_mode_type must be one of 'GeoRedundant', 'LocallyRedundant' or 'ZoneRedundant'"
  }
}

variable "cross_region_restore_enabled" {
  type        = bool
  description = "Whether to enable cross region restore for Recovery Services Vault. For this to be true var.storage_mode_type must be set to GeoRedundant"
  default     = false
}

variable "immutability" {
  type        = string
  description = "Whether you want vault to be immutable. Allowed values are: 'Locked', 'Unlocked' or 'Disabled'."
  default     = "Disabled"

  validation {
    condition     = contains(["Locked", "Unlocked", "Disabled"], var.immutability)
    error_message = "var.immutability must be one of 'Locked', 'Unlocked' or 'Disabled'"
  }
}

### Recovery Services Vault monitoring configuration
variable "rsv_alerts_for_all_job_failures_enabled" {
  type        = bool
  description = "Enabling/Disabling built-in Azure Monitor alerts for security scenarios and job failure scenarios."
  default     = true
}

variable "rsv_alerts_for_critical_operation_failures_enabled" {
  type        = bool
  description = "Enabling/Disabling alerts from the older (classic alerts) solution."
  default     = true
}

## Backup policy and direct assignment configuration
variable "backup_policies_vm" {
  description = "A map of backup policy objects where the key is the name of the policy."
  type = map(object({
    timezone                       = optional(string, "UTC") # [Allowed values](https://jackstromberg.com/2017/01/list-of-time-zones-consumed-by-azure/)
    backup_time                    = string                  # Time of day to perform backup in 24h format, e.g. 23:00
    backup_frequency               = string                  # Frequency of backup, supported values 'Hourly', 'Daily', 'Weekly'
    policy_type                    = optional(string, "V2")  # set to V1 or V2, see [here](https://learn.microsoft.com/en-us/azure/backup/backup-azure-vms-enhanced-policy?tabs=azure-portal)
    instant_restore_retention_days = optional(number)        # Between 1-5 for var.policy_type V1, 1-30 for V2
    backup_hour_interval           = optional(number)        # Interval of which backup is triggered. Allowed values are: 4, 6, 8 or 12. Used if backup_frequency is set to Hourly.
    backup_hour_duration           = optional(number)        # Duration of the backup window in hours. Value between 4 and 24. Used if backup_frequency is Hourly. Must be a multiplier of backup_hour_interval
    retention = optional(object({
      daily_backups_retention = optional(number) # Number of daily backups to retain, must be between 7-9999. Required if backup_frequency is Daily

      weekly_backups_retention = optional(number)       # Number of weekly backups to retain, must be between 1-9999. 
      weekdays                 = optional(list(string)) # The day in the week of backups to retain. Used for weekly retention.

      monthly_backups_retention = optional(number)       # Number of monthly backups to retain, must be between 1-9999. 
      months_weekdays           = optional(list(string)) # The day in the week of backups to retain. Used for monthly retention configuration
      months_weeks              = optional(list(string)) # Weeks of the month to retain backup of. Must be First, Second, Third or Last. Used for monthly retention configuration
      months_days               = optional(list(number)) # The days in the month to retain backups of. Must be between 1-31. Used for monthly retenion configuration
      months_include_last_days  = optional(bool, false)  # Whether to include last day of month, used if either months_weekdays, months_weeks or months_days is set. 

      yearly_backups_retention = optional(number)       # Number of yearly backups to retain, must be between 1-9999. 
      yearly_months            = optional(list(string)) # The months of the year to retain backups of. Values most be names of the month with capital case. Used for yearly retention configuration
      yearly_weekdays          = optional(list(string)) # The day in the week of backups to retain. Used for yearly retention configuration
      yearly_weeks             = optional(list(string)) # Weeks of the month to retain backup of. Must be First, Second, Third or Last. Used for yearly retention configuration
      yearly_days              = optional(list(number)) # The days in the month to retain backups of. Must be between 1-31. Used for monthly retention configuration
      yearly_include_last_days = optional(bool, false)  # Whether to include last day of month, used if either months_weekdays, months_weeks or months_days is set. 

    }))
  }))
}

variable "tags" {
  type        = map(string)
  description = "(Optional) Tags that will be applied to all deployed resources."
  default     = {}
}
```

***vm_backup_enable.tf***

Aqui pessoal eu irei demonstrar de forma simples como Ã© feito para habilitar o backup em uma mÃ¡quina virtual no cofre de backup e polÃ­tica criado acima com terraform:

```hcl
data "azurerm_virtual_machine" "vm" {
  name                = "vm-financeira01"
  resource_group_name = azurerm_resource_group.this.name
}

resource "azurerm_backup_protected_vm" "vm1" {
  resource_group_name = azurerm_resource_group.this.name.           # Resource Group onde estÃ¡ o cofre de backup
  recovery_vault_name = azurerm_recovery_services_vault.this.name.  # Nome do cofre de backup
  source_vm_id        = data.azurerm_virtual_machine.vm.id          # ID da VM que desejamos associar/habilitar o backup, estÃ¡ sendo recuperada a partir do bloco Data acima
  backup_policy_id    = azurerm_backup_policy_vm.backup_policy.id.  # ID dapolÃ­tica de backup
}
```

## Como usar os mÃ³dulos Terraform

Em Terraform temos o que Ã© chamado de stack que seria o arquivo onde Ã© feita a chamada aos mÃ³dulos/recursos que vocÃª quer ter no seu deployment, para usarmos esses mÃ³dulos geralmente criamos um arquivo main.tf fora da pasta mÃ³dulos (root folder) onde iremos fazer a chamada aos mÃ³dulos criados:

```hcl
module "rsv_ne_vm" {
  source = "./modules/az_recovery_services_vault"

  location            = var.location
  environment         = var.environment
  resource_group_name = azurerm_resource_group.this.name
  backup_type         = "vm"
  tags                = var.tags

  backup_policies_vm = {
    "bkpol-vm-${var.environment}" = {
      backup_time      = "20:00"
      backup_frequency = "Daily"

      instant_restore_retention_days = 3

      retention = {
        daily_backups_retention  = 8
      }
    }
  }
}
```

## OrganizaÃ§Ã£o do Terraform ao longo dos artigos

Seguindo a mesma premissa de outros artigos e seguindo tambÃ©m uma melhora prÃ¡tica que Ã© criar mÃ³dulos reutilizÃ¡veis de recursos, para esse nosso artigo terÃ¡ os mÃ³dulos de: **recovery services vault e politÃ­ca de backup**. 

- **main.tf**: onde serÃ¡ criado o/os recursos
- **locals.tf**: para manter alguns valores que usaremos no restante do cÃ³digo
- **variables.tf**: onde ficarÃ¡ as variÃ¡veis necessÃ¡rias
- **output.tf**: as saÃ­das dos recursos para serem reutilizados por outros recursos

```shell
ðŸ“¦modules
 â”— ðŸ“‚entra_id_groups
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ ðŸ“‚az_role_assignment
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ ðŸ“‚az_virtual_network
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ ðŸ“‚az_vnet_peering
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
 â”ƒ ðŸ“‚az_recovery_services_vault
 â”ƒ â”£ ðŸ“œlocals.tf
 â”ƒ â”£ ðŸ“œmain.tf
 â”ƒ â”£ ðŸ“œoutput.tf
 â”ƒ â”— ðŸ“œvariables.tf
```

## Concluindo!

Neste artigo, vimos como Ã© possÃ­vel criar um Azure Recovery Services Vault e configurar uma polÃ­tica de backup para mÃ¡quinas virtuais utilizando Terraform. Essa abordagem mostra na prÃ¡tica como a infraestrutura como cÃ³digo (IaC) simplifica tarefas que, manualmente, seriam mais demoradas e suscetÃ­veis a erros.

Adotar Terraform para gerenciar backups no Azure nÃ£o Ã© apenas uma questÃ£o de praticidade, mas tambÃ©m de maturidade operacional. Essa prÃ¡tica ajuda a construir uma base sÃ³lida de governanÃ§a e seguranÃ§a, reduzindo riscos e permitindo que as equipes foquem em inovaÃ§Ã£o e melhoria contÃ­nua.

Bom pessoal, eu tenho usado isso em alguns ambientes e acredito que possa ser bem Ãºtil a vocÃªs!

## Artigos relacionados

<a href="https://learn.microsoft.com/en-us/azure/backup/backup-overview" target="_blank">What is the Azure Backup service?</a>

<a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/recovery_services_vault" target="_blank">Manages a Recovery Services Vault.</a>

<a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/backup_policy_vm" target="_blank">Manages an Azure Backup VM Backup Policy.</a>

<a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/backup_protected_vm" target="_blank">Manages an Azure Backup Protected Virtual Machine.</a>

Compartilhe o artigo com seus amigos clicando nos icones abaixo!!!